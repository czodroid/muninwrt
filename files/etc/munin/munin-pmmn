#! /usr/bin/env perl
#
# Filename: munin-pmmn
# Author: Olivier Sirol <czo@free.fr>
# File Created: 20 April 2014
# Last Modified: Sunday 06 July 2025, 10:02
# Description:
#
#  A Poor Man's Munin Node that only uses warnings and strict as modules.
#  Original (c) 2012 LGPL - Steve Schnepp <steve.schnepp@pwkf.org>
#
#  There's no options, so you need to configure in this code:
#   $pmmn_dir: the directory before the plugins dir
#   $spoolfetch-dir: spoolfetch plugin dirs
#   $host: the hostname (but set as uname -n)
#
#   On OpenWRT you need to install perl and maybe
#   perlbase-getopt and perlbase-file
#
# Copyright: (C) 2014-2025 Olivier Sirol <czo@free.fr>

use warnings;
use strict;

## Config

my $VERSION = "1.0.1";

# my $pmmn_dir = "/home/tc/munin";
# my $pmmn_dir = "/Users/czo/Desktop/dev/munin";
my $pmmn_dir = "/etc/munin";

# my $spoolfetch_dir = $pmmn_dir . '/munin-async';
my $spoolfetch_dir;

# my $host = 'lagavulin';
my $host = `uname -n`;
chomp($host);

## End Config

my $plugin_dir = $pmmn_dir . "/plugins";

$| = 1;

{
    # PERL INIT

    # plugins/plugin.sh and Munin/Plugin.pm for plugins
    # export MUNIN_LIBDIR=/etc/munin ; export PERL5LIB=/etc/munin
    $ENV{'MUNIN_LIBDIR'} = $pmmn_dir;
    $ENV{'PERL5LIB'}     = $pmmn_dir;
}

print "# munin node at $host\n";

while ( my $line = <> ) {

    #chomp($line);
    $line =~ s,\r,,;
    $line =~ s,\n,,;
    my ( $cmd, $arg ) = split( / /, $line, 2 );
    $arg ||= "";
    my $plugin_filename = $plugin_dir . "/" . $arg;
    if ( !$cmd ) { next; }
    if ( $cmd eq "version" ) {
        print "munins node on $host version: $VERSION";
        next;
    } elsif ( $cmd eq "nodes" ) {
        print "$host\n";
        print ".";
        next;
    } elsif ( $cmd eq "quit" ) {
        exit(0);
    } elsif ( $cmd eq "list" ) {
        opendir( PLUGIN_DIR, $plugin_dir ) or die("cannot open: $@");
        while ( my $plugin = readdir(PLUGIN_DIR) ) {
            chomp($plugin);
            if ( $plugin =~ m/^\./ ) { next; }
            next unless ( -x "$plugin_dir/$plugin" );
            print "$plugin ";
        }
        closedir(PLUGIN_DIR);
        next;
    } elsif ( $cmd eq "config" || $cmd eq "alert" || $cmd eq "fetch" ) {
        if ( -d $plugin_filename || !-x $plugin_filename ) {
            print "# Unknown plugin [$arg] for $cmd";
            next;
        }
        my $arg_plugin = ( $cmd eq "fetch" ) ? "" : $cmd;
        system( $plugin_filename, $arg_plugin );
        print ".";
        next;
    } elsif ( $cmd eq "cap" ) {
        print "cap ";
        print "spool " if $spoolfetch_dir;
        next;
    } elsif ( $cmd eq "spoolfetch" && $spoolfetch_dir ) {
        system( "$spoolfetch_dir/spoolfetch_$host", $arg );
        print ".";
        next;
    }

    # Arriving here is not a good sign
    print "# Unknown command. Try list, nodes, config, fetch, version, alert or quit";
} continue {
    print "\n";
}

__END__
