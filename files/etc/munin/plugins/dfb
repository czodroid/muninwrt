#! /usr/bin/env perl
#
# Filename: dfb
# Author: Olivier Sirol <czo@free.fr>
# License: GPL-2.0 (http://www.gnu.org/copyleft)
# File Created: 03 Dec 2019
# Last Modified: Sunday 06 July 2025, 10:07
# $Id: dfb,v 1.42 2025/07/06 10:07:52 czo Git $
# Edit Time: 3:25:11
# Description:
#
#           Munin plugin
#           monitor disk usage in bytes
#
# Copyright: (C) 2019-2025 Olivier Sirol <czo@free.fr>

use strict;
use warnings;

my $warning;
my $critical;

## use or comment
$warning  = 92;
$critical = 98;

## Exclude name or mountpoint or type
# debian
# my $exclude = 'none|unknown|rootfs|iso9660|squashfs|udf|romfs|ramfs|debugfs|cgroup_root|devtmpfs|nfs';
# openwrt
my $exclude = 'overlayfs:|mtdblock15|tmpfs|nfs|\/snap\/';
## For these devices use the mount point, the device is useless
my %usemntpt = ( tmpfs => 1, none => 1, udev => 1, simfs => 1 );
my %dfh;
my $max = 0;

##===================================================================##
## Function

sub clean_fieldname ($) {
    my $name = shift;
    $name =~ s/^[^A-Za-z_]+/_/;
    $name =~ s/[^A-Za-z0-9_]/_/g;
    $name = "__root" if $name eq "root";
    return $name;
}

sub kilo {
    my $kilo;
    my $SizeInByte;
    my $SizeInKilo;
    my $SizeInMeg;
    my $SizeInGig;
    my $SizeInTB;

    ($SizeInByte) = @_;
    $SizeInKilo = sprintf( "%.0f", $SizeInByte / 1024.0 );
    $SizeInMeg  = sprintf( "%.0f", $SizeInByte / 1024.0 / 1024 );
    $SizeInGig  = sprintf( "%.1f", $SizeInByte / 1024.0 / 1024 / 1024 );
    $SizeInTB   = sprintf( "%.1f", $SizeInByte / 1024.0 / 1024 / 1024 / 1024 );

    if ( $SizeInKilo < 1000 ) {
        $kilo = $SizeInKilo . " KB";
    } else {
        if ( $SizeInMeg < 1000 ) {
            $kilo = $SizeInMeg . " MB";
        } else {
            if ( $SizeInGig < 1000 ) {
                $kilo = $SizeInGig . " GB";
            } else {
                $kilo = $SizeInTB . " TB";
            }
        }
    }
    return $kilo;
}

##===================================================================##
## Main

qx(which df 2>/dev/null) or die "ERROR: `df' not found\n";

## df:
# -P: posix
# -T: print the filesytem type
# -k: use 1k blocks (freeBSD uses 512 blocks)
my $headerline = 1;
foreach (qx(df -P -T -k)) {
    chomp;
    if ($headerline) { $headerline = 0; next; }
    my ( $name, $type, $size, $used, $avail, $cap, $mountpt ) = split( /\s+/, $_, 7 );
    ## print "$name, $type, $size, $used, $avail, $cap, $mountpt\n";
    next if ( $name =~ $exclude or $mountpt =~ $exclude or $type =~ $exclude );

    $name = $mountpt if defined( $usemntpt{$name} ) && $usemntpt{$name};
    $name = clean_fieldname($name);

    $dfh{$name}{type}    = $type;
    $dfh{$name}{size}    = $size;
    $dfh{$name}{used}    = $used;
    $dfh{$name}{avail}   = $avail;
    $dfh{$name}{cap}     = $cap;
    $dfh{$name}{mountpt} = $mountpt;

    $max = ( $size * 1024 ) if $max < ( $size * 1024 );
}

if ( $ARGV[0] and $ARGV[0] eq "config" ) {
    print <<EOT;
graph_title Disk usage (in bytes)
graph_category disk
graph_vlabel bytes
graph_args --base 1024 --lower-limit 0 --upper-limit $max
graph_info This graph lets you monitor absolute disk usage in bytes
EOT

    foreach my $df ( sort keys %dfh ) {
        print "$df.label $dfh{$df}{mountpt}\n";
        print "$df.cdef $df,1024,*\n";
        print "$df.info Max size: " . kilo( $dfh{$df}{size} * 1024 ) . " (" . $dfh{$df}{size} . " KB)\n";
        if ( $dfh{$df}{mountpt} ne '/rom' ) {
            print "$df.warning " . int( $dfh{$df}{size} * $warning / 100 ) . "\n";
            print "$df.critical " . int( $dfh{$df}{size} * $critical / 100 ) . "\n";
        }
    }
    exit 0;
}

foreach my $df ( sort keys %dfh ) {
    print "$df.value " . int( $dfh{$df}{used} ) . "\n" if $dfh{$df}{used};
}

__END__
